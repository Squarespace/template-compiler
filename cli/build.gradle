
description = 'Template compiler command line'

apply plugin: 'application'

dependencies {
  compile project(':template-core')
  compile project(':template-plugins-squarespace')
  compile "net.sourceforge.argparse4j:argparse4j:${argparseVersion}"

  testCompile project(path: ':template-core', configuration: 'tests')
  testCompile "org.testng:testng:${testngVersion}"
}

mainClassName = 'com.squarespace.template.cli.TemplateC'

task execJar(type: Jar, dependsOn: classes) {
  classifier = 'exec'
  manifest {
    attributes 'Main-Class': "${mainClassName}"
  }
  from files(sourceSets.main.output.classesDirs)
  from files(sourceSets.main.output.resourcesDir)
  from {
    configurations.runtime.collect {
      it.isDirectory() ? it : zipTree(it)
    }
  }
}

jar {
  manifest {
    attributes 'Main-Class': "${mainClassName}"
  }
}

task makeCli(type: Exec, dependsOn: execJar,
    description: 'Creates executable "templatec" command') {

  def inputScript = "src/main/resources/scripts/templatec.in"
  def inputArchive = execJar.archivePath
  def destScript = rootProject.file('templatec')
  workingDir '.'
  executable 'bash'
  args '-c', "cat ${inputScript} ${inputArchive} >${destScript}; chmod ug+x ${destScript}"
}

artifacts {
  archives sourcesJar
  archives testJar
  archives javadocJar
  archives execJar
}

