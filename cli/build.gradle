
description = 'Template compiler command line'

apply plugin: 'application'

dependencies {
  implementation project(':template-core')
  implementation "net.sourceforge.argparse4j:argparse4j:${argparseVersion}"

  testImplementation project(path: ':template-core', configuration: 'tests')
  testImplementation "org.testng:testng:${testngVersion}"
}

application {
  mainClassName = 'com.squarespace.template.cli.TemplateC'
}

task execJar(type: Jar, dependsOn: classes) {
  archiveClassifier = 'exec'
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE
  manifest {
    attributes 'Main-Class': "${mainClassName}"
  }
  from files(sourceSets.main.output.classesDirs)
  from files(sourceSets.main.output.resourcesDir)
  from {
    configurations.runtimeClasspath.collect {
      it.isDirectory() ? it : zipTree(it)
    }
  }
}

jar {
  manifest {
    attributes('Main-Class': "${mainClassName}")
  }
}

task makeCli(type: Exec, dependsOn: execJar,
    description: 'Creates executable "templatec" command') {

  def inputScript = "src/main/resources/scripts/templatec.in"
  def inputArchive = execJar.archiveFile
  def destScript = rootProject.file('templatec')
  workingDir '.'
  executable 'bash'
  args '-c', "cat ${inputScript} ${inputArchive} >${destScript}; chmod ug+x ${destScript}"
}
